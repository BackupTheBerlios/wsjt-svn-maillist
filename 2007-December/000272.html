<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<HTML>
 <HEAD>
   <TITLE> [WSJT-SVN] r587 - trunk
   </TITLE>
   <LINK REL="Index" HREF="http://lists.berlios.de/pipermail/wsjt-svn/2007-December/index.html" >
   <LINK REL="made" HREF="mailto:wsjt-svn%40lists.berlios.de?Subject=Re%3A%20%5BWSJT-SVN%5D%20r587%20-%20trunk&In-Reply-To=%3C200712141529.lBEFTfcR020620%40sheep.berlios.de%3E">
   <META NAME="robots" CONTENT="index,nofollow">
   <style type="text/css">
       pre {
           white-space: pre-wrap;       /* css-2.1, curent FF, Opera, Safari */
           }
   </style>
   <META http-equiv="Content-Type" content="text/html; charset=us-ascii">
   <LINK REL="Previous"  HREF="000271.html">
   <LINK REL="Next"  HREF="000273.html">
 </HEAD>
 <BODY BGCOLOR="#ffffff">
   <H1>[WSJT-SVN] r587 - trunk</H1>
    <B>k1jt at mail.berlios.de</B> 
    <A HREF="mailto:wsjt-svn%40lists.berlios.de?Subject=Re%3A%20%5BWSJT-SVN%5D%20r587%20-%20trunk&In-Reply-To=%3C200712141529.lBEFTfcR020620%40sheep.berlios.de%3E"
       TITLE="[WSJT-SVN] r587 - trunk">k1jt at mail.berlios.de
       </A><BR>
    <I>Fri Dec 14 16:29:41 CET 2007</I>
    <P><UL>
        <LI>Previous message: <A HREF="000271.html">[WSJT-SVN] r586 - trunk
</A></li>
        <LI>Next message: <A HREF="000273.html">[WSJT-SVN] r588 - trunk
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#272">[ date ]</a>
              <a href="thread.html#272">[ thread ]</a>
              <a href="subject.html#272">[ subject ]</a>
              <a href="author.html#272">[ author ]</a>
         </LI>
       </UL>
    <HR>  
<!--beginarticle-->
<PRE>Author: k1jt
Date: 2007-12-14 16:29:36 +0100 (Fri, 14 Dec 2007)
New Revision: 587

Added:
   trunk/encode232.f
   trunk/fano232.f
Log:
Added Fortran code encode232.f and fano232.f, for future incorporation.


Added: trunk/encode232.f
===================================================================
--- trunk/encode232.f	2007-12-14 14:43:17 UTC (rev 586)
+++ trunk/encode232.f	2007-12-14 15:29:36 UTC (rev 587)
@@ -0,0 +1,26 @@
+      subroutine encode232(dat,nbytes,symbol,nsym)
+
+C  Convolutional encoder for a K=32, r=1/2 code.
+
+      integer*1 dat(nbytes)             !User data, packed 8 bits per byte
+      integer*1 symbol(nsym)            !Channel symbols, one bit per byte
+      include 'conv232.f'
+
+      nstate=0
+      k=0
+      do j=1,nbytes
+         do i=7,0,-1
+            nstate=ior(ishft(nstate,1),iand(ishft(dat(j),-i),1))
+            n=iand(nstate,npoly1)
+            n=ieor(n,ishft(n,-16))
+            k=k+1
+            symbol(k)=partab(iand(ieor(n,ishft(n,-8)),255))
+            n=iand(nstate,npoly2)
+            n=ieor(n,ishft(n,-16))
+            k=k+1
+            symbol(k)=partab(iand(ieor(n,ishft(n,-8)),255))
+         enddo
+      enddo
+
+      return
+      end

Added: trunk/fano232.f
===================================================================
--- trunk/fano232.f	2007-12-14 14:43:17 UTC (rev 586)
+++ trunk/fano232.f	2007-12-14 15:29:36 UTC (rev 587)
@@ -0,0 +1,159 @@
+      subroutine fano232(symbol,nbits,mettab,ndelta,maxcycles,
+     +   dat,ncycles,metric,ierr)
+
+C  Sequential decoder for K=32, r=1/2 convolutional code using 
+C  the Fano algorithm.  Translated from C routine for same purpose
+C  written by Phil Karn, KA9Q.
+
+      parameter (MAXBITS=103)
+      parameter (MAXDAT=(MAXBITS+7)/8)
+      integer*1 symbol(0:2*MAXBITS-1)     !Soft channel symbols (erase=128)
+      integer*1 dat(MAXDAT)               !Decoded user data, 8 bits per byte
+      integer mettab(0:255,0:1)           !Metric table
+
+C  These were the &quot;node&quot; structure in Karn's C code:
+      integer nstate(0:MAXBITS-1)      !Encoder state of next node
+      integer gamma(0:MAXBITS-1)       !Cumulative metric to this node
+      integer metrics(0:3,0:MAXBITS-1) !Metrics indexed by all possible Tx syms
+      integer tm(0:1,0:MAXBITS-1)      !Sorted metrics for current hypotheses
+      integer ii(0:MAXBITS-1)          !Current branch being tested
+
+      integer*1 i1a,i1b
+      logical noback
+      equivalence (i1a,i4a),(i1b,i4b)
+      include 'conv232.f'
+
+      ntail=nbits-31
+
+C  Compute all possible branch metrics for each symbol pair.
+C  This is the only place we actually look at the raw input symbols
+      i4a=0
+      i4b=0
+      do np=0,nbits-1
+         j=2*np
+         i1a=symbol(j)
+         i1b=symbol(j+1)
+         metrics(0,np) = mettab(i4a,0) + mettab(i4b,0)
+         metrics(1,np) = mettab(i4a,0) + mettab(i4b,1)
+         metrics(2,np) = mettab(i4a,1) + mettab(i4b,0)
+         metrics(3,np) = mettab(i4a,1) + mettab(i4b,1)
+      enddo
+
+      np=0
+      nstate(np)=0
+
+C  Compute and sort branch metrics from the root node
+      n=iand(nstate(np),npoly1)
+      n=ieor(n,ishft(n,-16))
+      lsym=partab(iand(ieor(n,ishft(n,-8)),255))
+      n=iand(nstate(np),npoly2)
+      n=ieor(n,ishft(n,-16))
+      lsym=lsym+lsym+partab(iand(ieor(n,ishft(n,-8)),255))
+      m0=metrics(lsym,np)
+      m1=metrics(ieor(3,lsym),np)
+      if(m0.gt.m1) then
+         tm(0,np)=m0                      !0-branch has better metric
+         tm(1,np)=m1
+      else
+         tm(0,np)=m1                      !1-branch is better
+         tm(1,np)=m0
+         nstate(np)=nstate(np) + 1        !Set low bit
+      endif
+
+C  Start with best branch
+      ii(np)=0
+      gamma(np)=0
+      nt=0
+
+C  Start the Fano decoder
+      do i=1,nbits*maxcycles
+C  Look forward
+         ngamma=gamma(np) + tm(ii(np),np)
+         if(ngamma.ge.nt) then
+
+C  Node is acceptable.  If first time visiting this node, tighten threshold:
+            if(gamma(np).lt.(nt+ndelta)) nt=nt + 
+     +                            ndelta * ((ngamma-nt)/ndelta)
+
+C  Move forward
+            gamma(np+1)=ngamma
+            nstate(np+1)=nstate(np)+nstate(np)
+            np=np+1
+            if(np.eq.nbits-1) go to 100     !We're done!
+
+            n=iand(nstate(np),npoly1)
+            n=ieor(n,ishft(n,-16))
+            lsym=partab(iand(ieor(n,ishft(n,-8)),255))
+            n=iand(nstate(np),npoly2)
+            n=ieor(n,ishft(n,-16))
+            lsym=lsym+lsym+partab(iand(ieor(n,ishft(n,-8)),255))
+            
+            if(np.ge.ntail) then
+               tm(0,np)=metrics(lsym,np)      !We're in the tail, all zeros
+            else
+               m0=metrics(lsym,np)
+               m1=metrics(ieor(3,lsym),np)
+               if(m0.gt.m1) then
+                  tm(0,np)=m0                 !0-branch has better metric
+                  tm(1,np)=m1
+               else
+                  tm(0,np)=m1                 !1-branch is better
+                  tm(1,np)=m0
+                  nstate(np)=nstate(np) + 1   !Set low bit
+               endif
+            endif
+
+            ii(np)=0                          !Start with best branch
+            go to 99
+         endif
+
+C  Threshold violated, can't go forward
+ 10      noback=.false.
+         if(np.eq.0) noback=.true.
+         if(np.gt.0) then
+            if(gamma(np-1).lt.nt) noback=.true.
+         endif
+
+         if(noback) then
+C  Can't back up, either.  Relax threshold and look forward again 
+C  to a better branch.
+            nt=nt-ndelta
+            if(ii(np).ne.0) then
+               ii(np)=0
+               nstate(np)=ieor(nstate(np),1)
+            endif
+            go to 99
+         endif
+
+C  Back up
+         np=np-1
+         if(np.lt.ntail .and. ii(np).ne.1) then
+C  Search the next best branch
+            ii(np)=ii(np)+1
+            nstate(np)=ieor(nstate(np),1)
+            go to 99
+         endif
+         go to 10
+ 99      continue
+!         print*,np,nt,gamma(np)
+      enddo
+      i=nbits*maxcycles
+
+ 100  metric=gamma(np)                       !Final path metric
+
+C  Copy decoded data to user's buffer
+      nbytes=(nbits+7)/8
+      np=7
+      do j=1,nbytes-1
+         i4a=nstate(np)
+         dat(j)=i1a
+         np=np+8
+      enddo
+      dat(nbytes)=0
+
+      ncycles=i+1
+      ierr=0
+      if(i.ge.maxcycles*nbits) ierr=-1
+
+      return
+      end


</PRE>

<!--endarticle-->
    <HR>
    <P><UL>
        <!--threads-->
	<LI>Previous message: <A HREF="000271.html">[WSJT-SVN] r586 - trunk
</A></li>
	<LI>Next message: <A HREF="000273.html">[WSJT-SVN] r588 - trunk
</A></li>
         <LI> <B>Messages sorted by:</B> 
              <a href="date.html#272">[ date ]</a>
              <a href="thread.html#272">[ thread ]</a>
              <a href="subject.html#272">[ subject ]</a>
              <a href="author.html#272">[ author ]</a>
         </LI>
       </UL>

<hr>
<a href="https://lists.berlios.de/mailman/listinfo/wsjt-svn">More information about the wsjt-svn
mailing list</a><br>
</body></html>
